grammar su.nsk.iae.post.PoST hidden(WS, ML_COMMENT, SL_COMMENT)

generate poST "http://www.nsk.su/iae/post/PoST"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	(
		globVars+=GlobalVarDeclaration |
		programs+=Program |
		fbs+=FunctionBlock
	)*;
	
/* ======================= START Program ======================= */
	
Program: 
	'PROGRAM' name=ID
		(
			progInVars+=InputVarDeclaration | 
			progOutVars+=OutputVarDeclaration |
			progInOutVars+=InputOutputVarDeclaration |
			progVars+=VarDeclaration |
			progTempVars+=TempVarDeclaration |
			progExternVars+=ExternalVarDeclaration
		)*
		(processes+=Process)*
	'END_PROGRAM'
;

/* ======================= END Program ======================= */

/* ======================= START FB ======================= */
	
FunctionBlock: 
	'FUNCTION_BLOCK' name=ID
		(
			fbInVars+=InputVarDeclaration | 
			fbOutVars+=OutputVarDeclaration |
			fbInOutVars+=InputOutputVarDeclaration |
			fbVars+=VarDeclaration |
			fbTempVars+=TempVarDeclaration |
			fbExternVars+=ExternalVarDeclaration
		)*
		(processes+=Process)*
	'END_FUNCTION_BLOCK'
;

/* ======================= END FB ======================= */
	
/* ======================= START poST Expression ======================= */

SetStateStatement:
	{SetStateStatement} 'SET' ('STATE' state=[State] | next?='NEXT');

ProcessStatements:
	StartProcessStatement | StopProcessStatement | ErrorProcessStatement;

ProcessStatusExpression:
	'PROCESS' process=[Process] 'IN' 'STATE' (active?='ACTIVE' | inactive?="INACTIVE" | stop?='STOP' | error?='ERROR');

StartProcessStatement:
	{StartProcessStatement} ('START' 'PROCESS' process=[Process]) | {StartProcessStatement} 'RESTART';

StopProcessStatement:
	{StopProcessStatement} 'STOP' ('PROCESS' process=[Process])?;

ErrorProcessStatement:
	{ErrorProcessStatement} 'ERROR' ('PROCESS' process=[Process])?;

TimeoutStatement:
	'TIMEOUT' (const=Constant | variable=[SymbolicVariable]) 'THEN'
		statement=StatementList
	'END_TIMEOUT';

ResetTimerStatement:
	{ResetTimerStatement} 'RESET' 'TIMER';
	
Process: 
	'PROCESS' name=ID 
		(
			procVars+=VarDeclaration | 
			procTempVars+=TempVarDeclaration
		)*
		(states+=State)*
	'END_PROCESS';

State:
	'STATE' name=ID (looped?='LOOPED')?
		statement=StatementList
		timeout=TimeoutStatement?
	'END_STATE';
	

/* ======================= END poST Expression ======================= */
	
/* ======================= START ST Expression ======================= */

Expression:
	XorExpression ({Expression.left=current} OR_OPERATOR right=XorExpression)*;

terminal OR_OPERATOR:
	'OR';

XorExpression:
	AndExpression ({XorExpression.left=current} XOR_OPERATOR right=AndExpression)*;

terminal XOR_OPERATOR:
	'XOR';

AndExpression:
	CompExpression ({AndExpression.left=current} AND_OPERATOR right=CompExpression)*;

terminal AND_OPERATOR:
	'&' | 'AND';

CompExpression:
	EquExpression ({CompExpression.left=current} compOp=CompOperator right=EquExpression)*;

enum CompOperator:
	EQUAL='=' | NOT_EQUAL='<>';

EquExpression:
	AddExpression ({EquExpression.left=current} equOp=EquOperator right=AddExpression)*;

enum EquOperator:
	LESS='<' | GREATER='>' | LESS_EQU='<=' | GREATER_EQU='>=';

AddExpression:
	MulExpression ({AddExpression.left=current} addOp=AddOperator right=MulExpression)*;

enum AddOperator:
	PLUS='+' | MINUS='-';

MulExpression:
	PowerExpression ({MulExpression.left=current} mulOp=MulOperator right=PowerExpression)*;

enum MulOperator:
	MUL='*' | DIV='/' | MOD='MOD';

PowerExpression:
	UnaryExpression ({PowerExpression.left=current} POWER_OPERATOR right=UnaryExpression)*;

terminal POWER_OPERATOR:
	'**';

UnaryExpression:
	PrimaryExpression | (unOp=UnaryOperator right=PrimaryExpression);

enum UnaryOperator:
	NOT='NOT' | UNMINUS='-';

PrimaryExpression:
	const=Constant | variable=[SymbolicVariable] | array=ArrayVariable | procStatus=ProcessStatusExpression | '(' nestExpr=Expression ')';

StatementList:
	{StatementList} (statements+=Statement)*;

Statement:
	(AssignmentStatement ';') |
	SelectionStatement |
	IterationStatement |
	(SubprogramControlStatement ';') |
	(ExitStatement ';') |
	(ProcessStatements ';') |
	(SetStateStatement ';') |
	(ResetTimerStatement ';');

AssignmentStatement:
	(variable=[SymbolicVariable] | array=ArrayVariable) ':=' value=Expression;

SelectionStatement:
	IfStatement | CaseStatement;

IfStatement:
	'IF' mainCond=Expression 'THEN'
		mainStatement=StatementList
	('ELSIF' elseIfCond+=Expression 'THEN'
		elseIfStatements+=StatementList
	)*
	('ELSE'
		elseStatement=StatementList
	)?
	'END_IF';

CaseStatement:
	'CASE' cond=Expression 'OF'
		(caseElements+=CaseElement)+
	('ELSE'
		elseStatement=StatementList
	)?
	'END_CASE';

CaseElement:
	caseList=CaseList ':' statement=StatementList;

CaseList:
	caseListElement+=SignedInteger (',' caseListElement+=SignedInteger)*;

IterationStatement:
	ForStatement | WhileStatement | RepeatStatement;

ForStatement:
	'FOR' variable=[SymbolicVariable] ':=' forList=ForList 'DO'
		statement=StatementList
	'END_FOR';

ForList:
	start=Expression 'TO' end=Expression ('BY' step=Expression)?;

WhileStatement:
	'WHILE' cond=Expression 'DO'
		statement=StatementList
	'END_WHILE';

RepeatStatement:
	'REPEAT'
		statement=StatementList
	'UNTIL' cond=Expression 'END_REPEAT';

SubprogramControlStatement:
	{SubprogramControlStatement} 'RETURN';
	

ExitStatement:
	{ExitStatement} 'EXIT';
	
ArrayVariable:
	varName=[SymbolicVariable] '[' index=Expression ']';

/* ======================= END ST Expression ======================= */
	
/* ======================= START Variables ======================= */

SymbolicVariable:
	name=ID;

terminal DIRECT_VARIABLE returns ecore::EString:
	'%' DIRECT_TYPE_PREFIX DIRECT_SIZE_PREFIX INTEGER ('.' INTEGER)*;

terminal fragment DIRECT_TYPE_PREFIX:
	'I' | 'Q' | 'M';

terminal fragment DIRECT_SIZE_PREFIX:
	'X' | 'B' | 'W' | 'D' | 'L';

VarInitDeclaration:
	varList=VarList ':' (spec=SimpleSpecificationInit | arrSpec=ArraySpecInit);

VarList:
	vars+=SymbolicVariable (',' vars+=SymbolicVariable)*;

InputVarDeclaration:
	'VAR_INPUT'
		(vars+=VarInitDeclaration ';')+
	'END_VAR';

OutputVarDeclaration:
	'VAR_OUTPUT'
		(vars+=VarInitDeclaration ';')+
	'END_VAR';

InputOutputVarDeclaration:
	'VAR_IN_OUT'
		(vars+=VarInitDeclaration ';')+
	'END_VAR';

VarDeclaration:
	'VAR' (const?='CONSTANT')?
		(vars+=VarInitDeclaration ';')+
	'END_VAR';

TempVarDeclaration:
	'VAR_TEMP'
		(vars+=VarInitDeclaration ';')+
	'END_VAR';

ExternalVarDeclaration:
	'VAR_EXTERNAL' (const?='CONSTANT')?
		(vars+=ExternalVarInitDeclaration ';')+
	'END_VAR';

ExternalVarInitDeclaration:
	varList=VarList ':' type=DataTypeName;

GlobalVarDeclaration:
	'VAR_GLOBAL' (const?='CONSTANT')?
		(
			(varsSimple+=VarInitDeclaration ';') | 
			(varsAs+=GlobalVarInitDeclaration ';')
		)+
	'END_VAR';

GlobalVarInitDeclaration:
	varList=VarList 'AT' location=DIRECT_VARIABLE ':' type=DataTypeName;

/* ======================= END Variables ======================= */

/* ======================= START Array ======================= */
	
ArraySpecInit:
	init=ArraySpecification (':=' values=ArrayInitialization)?;

ArraySpecification:
	'ARRAY' '[' start=SignedInteger '..' end=SignedInteger ']' 'OF' type=DataTypeName;

ArrayInitialization :
	'[' elements+=Constant (',' elements+=Constant)* ']';

/* ======================= END Array ======================= */
	
/* ======================= START Time Literals ======================= */

TimeLiteral:
	 TIME_PREF_LITERAL '#' '-'? interval=INTERVAL
;

terminal TIME_PREF_LITERAL:
	'T';

terminal INTERVAL returns ecore::EString:
	(INTEGER 'd')? (INTEGER 'h')? (INTEGER 'm')? (INTEGER 's')? (INTEGER 'ms')?;

/* ======================= END Time Literals ======================= */
	
/* ======================= START Elementary data types ======================= */

DataTypeName returns ecore::EString:
	NumericTypeName | BIT_STRING_TYPE_NAME | TIME_TYPE_NAME| STRING_TYPE_NAME;

NumericTypeName returns ecore::EString:
	IntegerTypeName | REAL_TYPE_NAME;

IntegerTypeName returns ecore::EString:
	SIGNED_INTEGER_TYPE_NAME | UNSIGNED_INTEGER_TYPE_NAME;

terminal SIGNED_INTEGER_TYPE_NAME returns ecore::EString:
	'SINT' | 'INT' | 'DINT' | 'LINT';

terminal UNSIGNED_INTEGER_TYPE_NAME  returns ecore::EString:
	'USINT' | 'UINT' | 'UDINT' | 'ULINT';

terminal REAL_TYPE_NAME returns ecore::EString:
	'REAL' | 'LREAL';

terminal BIT_STRING_TYPE_NAME returns ecore::EString:
	'BOOL' | 'BYTE' | 'WORD' | 'DWORD' | 'LWORD';
	
terminal TIME_TYPE_NAME returns ecore::EString:
	'TIME';
	
terminal STRING_TYPE_NAME returns ecore::EString:
	'STRING' | 'WSTRING';

SimpleSpecificationInit:
	{SimpleSpecificationInit} type=DataTypeName (':=' value=Constant)?;

/* ======================= END Elementary data types ======================= */
	
/* ======================= START Numeric Literals ======================= */

Constant:
	NumericLiteral | 
	TimeLiteral | 
	{Constant} BINARY_INTEGER | 
	{Constant} OCTAL_INTEGER | 
	{Constant} HEX_INTEGER | 
	{Constant} BOOLEAN_LITERAL;

terminal INTEGER returns ecore::EInt:
	DIGIT+;

SignedInteger:
	(iSig?='-')? value=INTEGER;
	
terminal REAL returns ecore::EDouble:
	INTEGER '.' INTEGER;

terminal BINARY_INTEGER returns ecore::EString:
	'2#' BIT+;

terminal OCTAL_INTEGER returns ecore::EString:
	'8#' OCTAL_DIGIT+;

terminal HEX_INTEGER returns ecore::EString:
	'16#' HEX_DIGIT+;
	
NumericLiteral:
	IntegerLiteral | RealLiteral;

IntegerLiteral:
	(type=IntegerTypeName '#')? value=SignedInteger;

RealLiteral:
	(type=REAL_TYPE_NAME '#')? (rSig?='-')? value=REAL;

terminal BOOLEAN_LITERAL:
	'TRUE' | 'FALSE';

/* ======================= END Numeric Literals ======================= */
	
/* ======================= START Identifiers ======================= */

terminal fragment LETTER:
	'A'..'Z' | 'a'..'z' | '_';

terminal fragment BIT:
	'0' | '1';
	
terminal fragment OCTAL_DIGIT:
	'0'..'7';

terminal fragment DIGIT:
	'0'..'9';

terminal fragment HEX_DIGIT:
	DIGIT | 'A'..'F';

terminal ID returns ecore::EString:
	LETTER (LETTER | DIGIT)*;

terminal ML_COMMENT:
    '/*' -> '*/' | '(*' -> '*)';
 
terminal SL_COMMENT:
    '//' !('\n'|'\r')* ('\r'? '\n')?;
 
terminal WS:
    (' '|'\t'|'\r'|'\n')+;
 
terminal ANY_OTHER:
    .;

/* ======================= END Identifiers ======================= */
